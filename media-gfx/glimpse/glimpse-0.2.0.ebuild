# Copyright 1999-2021 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

# Based on media-gfx/gimp-2.10.18-r2.ebuild

EAPI=7
GNOME2_EAUTORECONF=yes
WANT_AUTOMAKE=

inherit autotools gnome2 virtualx

DESCRIPTION="Image editor based on the GNU Image Manipulation Program"
HOMEPAGE="https://glimpse-editor.org/"
SRC_URI="https://github.com/glimpse-editor/Glimpse/archive/v${PV}.tar.gz -> ${P}.tar.gz"

LICENSE="GPL-3+ LGPL-3+"
SLOT="0"
KEYWORDS="~amd64"

IUSE="aalib alsa aqua debug doc gnome heif jpeg2k mng openexr postscript test udev unwind vector-icons webp wmf xpm cpu_flags_ppc_altivec cpu_flags_x86_mmx cpu_flags_x86_sse"

RESTRICT="!test? ( test )"

S="${WORKDIR}/Glimpse-${PV}"

COMMON_DEPEND="
	>=app-text/poppler-0.50[cairo]
	>=app-text/poppler-data-0.4.7
	>=dev-libs/atk-2.2.0
	>=dev-libs/glib-2.56.0:2
	>=dev-libs/json-glib-1.2.6
	dev-libs/libxml2:2
	dev-libs/libxslt
	>=gnome-base/librsvg-2.40.6:2
	>=media-gfx/mypaint-brushes-1.3.1:=
	>=media-libs/babl-0.1.78
	>=media-libs/fontconfig-2.12.4
	>=media-libs/freetype-2.1.7
	>=media-libs/gegl-0.4.22:0.4[cairo]
	>=media-libs/gexiv2-0.10.6
	>=media-libs/harfbuzz-0.9.19
	>=media-libs/lcms-2.8:2
	>=media-libs/libmypaint-1.5.1:=
	>=media-libs/libpng-1.6.25:0=
	>=media-libs/tiff-3.5.7:0
	net-libs/glib-networking[ssl]
	sys-libs/zlib
	virtual/jpeg
	>=x11-libs/cairo-1.12.2
	>=x11-libs/gdk-pixbuf-2.31:2
	>=x11-libs/gtk+-2.24.32:2
	x11-libs/libXcursor
	>=x11-libs/pango-1.29.4
	aalib? ( media-libs/aalib )
	alsa? ( >=media-libs/alsa-lib-1.0.0 )
	aqua? ( >=x11-libs/gtk-mac-integration-2.0.0 )
	heif? ( >=media-libs/libheif-1.3.2:= )
	jpeg2k? ( >=media-libs/openjpeg-2.1.0:2= )
	mng? ( media-libs/libmng:= )
	openexr? ( >=media-libs/openexr-1.6.1:= )
	postscript? ( app-text/ghostscript-gpl )
	udev? ( dev-libs/libgudev:= )
	unwind? ( >=sys-libs/libunwind-1.1.0:= )
	webp? ( >=media-libs/libwebp-0.6.0:= )
	wmf? ( >=media-libs/libwmf-0.2.8 )
	xpm? ( x11-libs/libXpm )
"

RDEPEND="
	!media-gfx/gimp
	${COMMON_DEPEND}
	x11-themes/hicolor-icon-theme
	gnome? ( gnome-base/gvfs )
"

DEPEND="
	${COMMON_DEPEND}
	>=dev-lang/perl-5.10.0
	dev-libs/appstream-glib
	>=dev-util/intltool-0.40.1
	sys-apps/findutils
	>=sys-devel/gettext-0.19
	doc? ( dev-util/gtk-doc )
	test? ( x11-base/xorg-server[xvfb] )
"
BDEPEND="
	dev-util/gtk-update-icon-cache
	>=sys-devel/libtool-2.2
	virtual/pkgconfig
"

DOCS=( "AUTHORS" "HACKING.md" "NEWS" "README.md" )

# Bug 685210 (and duplicate bug #691070)
PATCHES=(
	"${FILESDIR}/${PN}-0.2_fix_test-appdata.patch"
)

src_prepare() {
	if has_version "media-gfx/mypaint-brushes:2.0" ; then
		sed -i -e 's/mypaint-brushes-1.0/mypaint-brushes-2.0/' configure.ac \
			|| die # bug #737794
	fi

	# Disable system CFLAGS suppressing on SSE{2,4.1} support tests by addition
	# of {SSE2,SSE4_1}_EXTRA_CFLAGS: bug #702554
	sed -i -e 's:\$intrinsics_save_CFLAGS \$SSE2_EXTRA_CFLAGS:\$SSE2_EXTRA_CFLAGS \$intrinsics_save_CFLAGS:' \
		-e 's:\$intrinsics_save_CFLAGS \$SSE4_1_EXTRA_CFLAGS:\$SSE4_1_EXTRA_CFLAGS \$intrinsics_save_CFLAGS:' configure.ac || die

	sed -i -e 's/== "xquartz"/= "xquartz"/' configure.ac || die # bug #494864
	sed 's:-DGIMP_DISABLE_DEPRECATED:-DGIMP_protect_DISABLE_DEPRECATED:g' \
		-i configure.ac || die # bug #615144

	# Would normally be generated by autogen.sh.
	! use doc && (echo "EXTRA_DIST = missing-gtk-doc" > gtk-doc.make || die)
	gnome2_src_prepare  # calls eautoreconf

	sed 's:-DGIMP_protect_DISABLE_DEPRECATED:-DGIMP_DISABLE_DEPRECATED:g' \
		-i configure || die # bug #615144
	fgrep -q GIMP_DISABLE_DEPRECATED configure || die # bug #615144, self-test

}

_adjust_sandbox() {
	# Bug #569738 and bug #591214
	local nv
	for nv in /dev/nvidia-uvm /dev/nvidiactl /dev/nvidia{0..9} ; do
		# We do not check for existence as they may show up later
		# https://bugs.gentoo.org/show_bug.cgi?id=569738#c21
		addwrite "${nv}"
	done

	addwrite /dev/dri/  # bug #574038 and bug #684886
	addwrite /dev/ati/  # bug #589198
	addwrite /proc/mtrr # bug #589198
}

src_configure() {
	_adjust_sandbox

	local myconf=(
		GEGL="${EPREFIX}"/usr/bin/gegl-0.4

		--enable-default-binary

		--disable-check-update
		--enable-mp
		--with-appdata-test
		--with-bug-report-url=https://bugs.gentoo.org/
		--with-xmc
		--without-libbacktrace
		--without-webkit
		--without-xvfb-run
		--disable-python
		$(use_enable cpu_flags_ppc_altivec altivec)
		$(use_enable cpu_flags_x86_mmx mmx)
		$(use_enable cpu_flags_x86_sse sse)
		$(use_enable vector-icons)
		$(use_with aalib aa)
		$(use_with alsa)
		$(use_with !aqua x)
		$(use_with heif libheif)
		$(use_with jpeg2k jpeg2000)
		$(use_with mng libmng)
		$(use_with openexr)
		$(use_with postscript gs)
		$(use_with udev gudev)
		$(use_with unwind libunwind)
		$(use_with webp)
		$(use_with wmf)
		$(use_with xpm libxpm)
	)

	gnome2_src_configure "${myconf[@]}"
}

src_compile() {
	export XDG_DATA_DIRS="${EPREFIX}"/usr/share  # bug 587004
	gnome2_src_compile
}

src_test() {
	virtx emake check
}

src_install() {
	gnome2_src_install

	# Workaround for bug #321111 to give Glimpse the least
	# precedence on PDF documents by default
	mv "${ED%/}"/usr/share/applications/{,zzz-}org.glimpse_editor.Glimpse.desktop || die

	find "${D}" -name '*.la' -type f -delete || die

	# Prevent dead symlink gimp-console.1 from downstream man page compression
	# (bug #433527)
	local gimp_app_version=$(grep '\[gimp_app_version' configure.ac \
								 | sed -E 's/.*\[([2-9]\.[0-9]+)\].*/\1/')
	mv "${ED%/}"/usr/share/man/man1/gimp-console{-${gimp_app_version},}.1 || die
}
